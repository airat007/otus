
#Область ОбработчикиСобытий
 
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Создание движений в регистре накопления ТоварыКПередаче
	Движения.ТоварыКПередаче.Записывать = Истина;
	
	Если Статус = Перечисления.СтатусыЗаявки.Согласована Тогда
		
		Для каждого СтрокаТовары Из Товары Цикл

			Движение = Движения.ТоварыКПередаче.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Период = Дата;
			Движение.Товар = СтрокаТовары.Товар;
			Движение.Заказ = Ссылка;
			Движение.Количество = СтрокаТовары.Количество;

		КонецЦикла;
		
	КонецЕсли;
		
КонецПроцедуры 

Процедура ПриЗаписи(Отказ)
	
	ЗаписатьВОчередьИсходящихСообщений = 
		ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Проведение 
		И ЗначениеЗаполнено(Заявка)
		И ДополнительныеСвойства.Статус <> Статус 
		И Статус <> Перечисления.СтатусыЗаявки.Новая; 
	
	Если ЗаписатьВОчередьИсходящихСообщений Тогда
		
		МенеджерЗаписи = РегистрыСведений.ОчередьИсходящихСообщенийЗаказов.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Заказ = Ссылка;
		МенеджерЗаписи.Состояние = Перечисления.СостоянияИсходящихСообщений.Новое; 
		
		МенеджерЗаписи.Записать();

	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	ДополнительныеСвойства.Вставить("Статус", Ссылка.Статус);
	
КонецПроцедуры

#КонецОбласти
